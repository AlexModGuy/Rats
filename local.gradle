buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
        mavenLocal()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

apply from: 'build.gradle'

import net.minecraftforge.gradle.common.util.RunConfig

class DefaultRunConfig extends RunConfig {

    DefaultRunConfig(Project project, SourceSet sourceSet, String n) {
        super(project, n)
        this.taskName "Minecraft_" + name.substring(0, 1).toUpperCase(Locale.US).concat(name.substring(1)) + "_" + project.minecraft_version
        //this.properties 'forge.logging.markers': 'SCAN,AXFORM'
        this.properties 'forge.logging.console.level': 'debug'
        this.properties 'mixin.env.disableRefMap': 'true'
        this.arg "-mixin.config=citadel.mixins.json"
        this.arg "-mixin.config=rats.mixins.json"
        this.workingDirectory project.file('run').canonicalPath
        this.source sourceSet
        this.environment 'TMP': './tmp/'
        this.ideaModule project.getName().concat(".main")
        this.singleInstance true
    }

    DefaultRunConfig setArgs(String... args) {
        setArgs(new ArrayList() {
            {
                for (String arg : args)
                    add(arg)
            }
        })
        return this
    }
}

class DataRunConfig extends DefaultRunConfig {

    DataRunConfig(Project project, SourceSet sourceSet) {
        super(project, sourceSet, "data")
        this.properties 'fml.earlyprogresswindow': 'false'
        this.properties 'mixin.env.disableRefMap': 'true'
    }

}

repositories {
    maven {
        url = uri("maven-local")
    }

    maven {
        url = "https://modmaven.dev/"
    }

    maven {
        url = "https://maven.tamaized.com/releases"
    }
}

dependencies {
    runtimeOnly fg.deobf("curse.maven:jade-324717:4573193")
    runtimeOnly fg.deobf("curse.maven:appleskin-248787:4605078")
    runtimeOnly fg.deobf("curse.maven:better-advancements-272515:4631253")
//    runtimeOnly fg.deobf("curse.maven:configured-457570:4462894")
    runtimeOnly fg.deobf("curse.maven:catalogue-459701:4590890")
    runtimeOnly fg.deobf("curse.maven:worldedit-225608:4586218")
    runtimeOnly fg.deobf("curse.maven:attributefix-280510:4588114")
    runtimeOnly fg.deobf("teamtwilight:twilightforest:4.3.1756:universal")
    runtimeOnly fg.deobf("gizmo:obtrophies:1.20-2.2.7")
    runtimeOnly fg.deobf("curse.maven:structure-expansion-398430:4598853")
    runtimeOnly fg.deobf("curse.maven:commandstructures-565119:4438632")
    runtimeOnly fg.deobf("io.github.flemmli97:debugutils:${project.minecraft_version}-${project.du_version}-forge")
    runtimeOnly fg.deobf("curse.maven:domestication-innovation-591149:4578807")
    runtimeOnly fg.deobf("curse.maven:wanna-skate-695018:4576733")
}

minecraft {
    getRuns().clear()
    var proj = project
    var main = sourceSets.main
    runs {
        add(new DefaultRunConfig(proj, main, "client").setArgs(

                '--username', 'GizmoTheMoonPig',

                '--uuid', 'c47fe203-99e7-45c4-9c19-2c9281b74364'

        ))
        add(new DefaultRunConfig(proj, main, "client").setArgs(

                '--username', 'Dev1'

        ))
        add(new DefaultRunConfig(proj, main, "client").setArgs(

                '--username', 'Dev2'

        ))
        add(new DefaultRunConfig(proj, main, "client").setArgs(

                '--username', 'Dev3'

        ))

        add(new DefaultRunConfig(proj, main, "server"))


        add(new DataRunConfig(proj, main).setArgs(
                '--mod', 'rats', '--all',
                '--existing', '"' + file('src/main/resources/') + '"',
                '--existing', '"' + file('src/generated/resources/') + '"',
                '--output', '"' + file('src/generated/resources/') + '"'
        ))

    }
}